---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "p0_gcp_access_logs Resource - p0"
subcategory: ""
description: |-
  An installation of P0, on a single Google Cloud project, for access-log collection,
  which enhances IAM assessment.
  To use this resource, you must also:
  install the p0_gcp_iam_assessment resource,create a Pub/Sub topic,create a logging sink, publishing to this topic,grant your logging service account permissions to publish to this Pub/Sub topic, andgrant P0 the ability to subscribe to this Pub/Sub topic.
  Use the read-only attributes defined on p0_gcp to create the requisite Google Cloud infrastructure.
  P0 recommends defining this infrastructure according to the example usage pattern.
---

# p0_gcp_access_logs (Resource)

An installation of P0, on a single Google Cloud project, for access-log collection,
which enhances IAM assessment.

To use this resource, you must also:
- install the `p0_gcp_iam_assessment` resource,
- create a Pub/Sub topic,
- create a logging sink, publishing to this topic,
- grant your logging service account permissions to publish to this Pub/Sub topic, and
- grant P0 the ability to subscribe to this Pub/Sub topic.

Use the read-only attributes defined on `p0_gcp` to create the requisite Google Cloud infrastructure.

P0 recommends defining this infrastructure according to the example usage pattern.

## Example Usage

```terraform
resource "p0_gcp" "example" {
  organization_id = "123456789012"
}

locals {
  project = "my_project_id"
}

# Follow instructions for creating Terraform for IAM assessment in p0_gcp_iam_assessment documentation
# ...

resource "p0_gcp_iam_assessment" "example" {
  project    = locals.project
  depends_on = [google_project_iam_member.example]
}

resource "google_project_iam_audit_config" "example" {
  project = locals.project
  service = "allServices"
  audit_log_config {
    log_type = "ADMIN_READ"
  }
  audit_log_config {
    log_type = "DATA_READ"
  }
  audit_log_config {
    log_type = "DATA_WRITE"
  }
}

# Data access logs are sent to P0 using this Pub/Sub topic
resource "google_pubsub_topic" "example" {
  project = locals.project
  name    = p0_gcp.example.access_logs.pub_sub.topic_id
}

# The log sink that writes to the P0 access-logging Pub/Sub topic
resource "google_logging_project_sink" "example" {
  project     = locals.project
  name        = p0_gcp.example.access_logs.logging.sink_id
  destination = "pubsub.googleapis.com/projects/my_project/topics/${google_pubsub_topic.example.name}"
  description = "P0 data access log sink"

  filter = p0_gcp.example.access_logs.logging.filter
}

# Grants the logging service account permission to write to the access-logging Pub/Sub topic
resource "google_pubsub_topic_iam_member" "logging_example" {
  project = locals.project
  role    = p0_gcp.example.access_logs.logging.role
  topic   = google_pubsub_topic.example.name
  member  = google_logging_project_sink.example.writer_identity
}

# Grants P0 permission to read from the access-logging Pub/Sub topic
resource "google_pubsub_topic_iam_member" "p0_example" {
  project = locals.project
  role    = p0_gcp.example.access_logs.predefined_role
  topic   = google_pubsub_topic.example.name
  member  = "serviceAccount:${p0_gcp.example.serviceAccountEmail}"
}

# Finish the P0 access-logs installation
resource "p0_gcp_access_logs" "example" {
  project = locals.project
  depends_on = [
    google_logging_project_sink.example,
    google_pubsub_topic_iam_member.p0_example
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `project` (String) The ID of the Google Cloud project to manage with P0

### Read-Only

- `state` (String) This item's install progress in the P0 application:
	- 'stage': The item has been staged for installation
	- 'configure': The item is available to be added to P0, and may be configured
	- 'installed': The item is fully installed
